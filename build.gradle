buildscript {
    repositories {
        if (rootProject.hasProperty('jmixPhoenixRepoUrl')) {
            maven {
                url rootProject['jmixPhoenixRepoUrl']

                if (rootProject.hasProperty('jmixPhoenixRepoUser') && rootProject.hasProperty('jmixPhoenixRepoPassword')) {
                    credentials {
                        username rootProject['jmixPhoenixRepoUser']
                        password rootProject['jmixPhoenixRepoPassword']
                    }
                }
            }
        }
        if (rootProject.hasProperty('jmixRepoUrl')) {
            if (rootProject.hasProperty('jmixUseLocalMavenRepository')) {
                mavenLocal()
            }
            maven {
                url rootProject['jmixRepoUrl']

                if (rootProject.hasProperty('jmixRepoUser') && rootProject.hasProperty('jmixRepoPassword')) {
                    credentials {
                        username rootProject['jmixRepoUser']
                        password rootProject['jmixRepoPassword']
                    }
                }
            }
            gradlePluginPortal()
        } else {
            mavenLocal()
            maven {
                url "https://nexus.jmix.io/repository/public"
            }
            gradlePluginPortal()
        }
    }
    dependencies {
        classpath("io.jmix.build:jmix-build:${rootProject.findProperty('buildPluginVersion') ?: version}")
        classpath("io.jmix.gradle:jmix-gradle-plugin:${rootProject.findProperty('jmixPluginVersion') ?: version}")
    }
}

configure(subprojects.findAll { it.name != 'bom' }) {
    apply(plugin: 'io.jmix.build')
}
